#!/usr/bin/env perl

=head1 NAME

occi - Adafruit Occidentalis Configuration Helper for Raspberry Pi

=head1 DESCRIPTION

occi is a simple utility for applying configuration settings like
hostname and WiFi credentials to your Raspberry Pi.

Settings are stored in a simple text file, usually
F</boot/occidentalis.txt>.  The format is:

    hostname=brennenpi

    # wifi configuration details:
    wifi_ssid=Uniontown Labs
    wifi_password=Code and Biko do not like cats.

Blank lines and comments starting with C<#> will be ignored.

See F</usr/share/doc/occi/occidentalis_example.txt> for a full example.

=head1 SYNOPSIS

    # apply configuration in /boot/occidentalis.txt
    sudo occi

=cut

package OcciConfig v0.3.4;

use warnings;
use strict;

use IPC::System::Simple qw(capturex);

diag(0, "Adafruit Occidentalis configuration helper, ${OcciConfig::VERSION}");

my $OCCI_CONFIG  = "/boot/occidentalis.txt";
my %config       = parse_config($OCCI_CONFIG);
my %allowed_keys = map { $_ => 1} qw(hostname wifi_password wifi_ssid);

have_config_or_die($OCCI_CONFIG);

diag(1, 'file', $OCCI_CONFIG);
my @errors;
foreach my $key (sort keys %config) {
  if ($allowed_keys{$key}) {
    diag(2, 'valid', $key, $config{$key});
  } else {
    diag(2, 'error', $key, "unrecognized configuration key");
  }
}

{
  # This bit of magic will find every sub starting with "handle_".
  # It just stands in for explicitly calling:
  #
  #   handle_hostname(%config);
  #   handle_wifi(%config);
  #
  # and so on down the line.

  no strict 'refs';
  my (@handlers) = grep { defined &{"OcciConfig\::$_"} && m/^handle_/ } keys %{"OcciConfig\::"};

  foreach my $handler (@handlers) {
    my @log = &{$handler}(%config);
    foreach my $logline (@log) {
      if (length($logline)) {
        diag(1, 'run', $handler, $logline);
      }
    }
  }
}

exit 0;

=head1 CONFIGURATION HANDLERS

To add a handler, just write a sub that takes the %config hash, like so,
and returns a list containing one or more log items:

    sub handle_foo {
      my %config = @_;
      return ('nothing to do here');
    }

It will automatically be called every time occi runs.

=over

=item handle_hostname()

Update current hostname and make sure it's set properly at boot.

=cut

sub handle_hostname {
  my %config = @_;

  return ('no hostname specified')
    unless defined $config{hostname};

  my @log;

  my $hostname_changed = 0;

  # What's the existing configuration?
  chomp(my $existing_etc_hostname = get_file('/etc/hostname'));
  chomp(my $existing_hostname = capture_string('hostname'));

  unless ($existing_etc_hostname eq $config{hostname}) {
    # Make sure this is set correctly at next boot
    push @log, 'Setting /etc/hostname to ' . $config{hostname};
    put_file('/etc/hostname', $config{hostname});
    $hostname_changed = 1;
  }

  unless ($existing_hostname eq $config{hostname}) {
    # Make sure this is set correctly right _now_.
    push @log, 'Setting current hostname to ' . $config{hostname};
    system('hostname', $config{hostname});
    $hostname_changed = 1;
  }

  # Make sure our new hostname is mentioned in /etc/hosts:
  my $etc_hosts = get_file('/etc/hosts');
  my $new_etc_hosts = $etc_hosts;
  my $config_hostline = "127.0.1.1\t$config{hostname}";
  $new_etc_hosts =~ s/^(127[.]0[.]1[.]1\s+${existing_hostname})$/$config_hostline/m;
  if ($new_etc_hosts !~ /$config_hostline/) {
    $new_etc_hosts .= "\n$config_hostline";
  }
  if ($etc_hosts ne $new_etc_hosts) {
    push @log, 'Adding ' . $config_hostline . ' to /etc/hosts';
    put_file('/etc/hosts', $new_etc_hosts);
    $hostname_changed = 1;
  }

  if ($hostname_changed && (-f '/etc/init.d/avahi-daemon')) {
    push @log, 'restarting avahi-daemon';
    push @log, capture_list('service', 'avahi-daemon', 'restart');
  }

  return @log;
}

=item handle_wifi()

=cut

sub handle_wifi {
  my %config = @_;

  my $conf_file = '/etc/wpa_supplicant/wpa_supplicant.conf';
  my $blurb = get_blurb();

  return ('no wifi_ssid specified')
    unless defined $config{wifi_ssid};

  my @log = 'Configuring ' . $config{wifi_ssid};

  my ($ifconfig) = capture_string('ifconfig', '-s');
  if ($ifconfig !~ /wlan/) {
    push @log, 'No wireless hardware found.';
  } elsif (defined $config{wifi_password}) {
    my $wpa_config = capture_string(
      'wpa_passphrase',
      $config{wifi_ssid},
      $config{'wifi_password'}
    );

    $wpa_config = <<"WPA";
# $blurb
ctrl_interface=DIR=/var/run/wpa_supplicant GROUP=netdev
update_config=1
$wpa_config
WPA

    push @log, "Writing $conf_file";

    push @log, capture_list(
      'wpa_cli',
      'reconfigure'
    );

    put_file($conf_file, $wpa_config, $conf_file . '.backup');
  } else {
    push @log, "No wifi_password defined, falling back to iwconfig.";
    my $iwlog = capture_string(
      'iwconfig',
      # TODO: this of course is not always going to be the same...
      'wlan0',
      'essid',
      $config{wifi_ssid}
    );

  }

  return @log;
}

=item capture_string($cmd, @args)

Return a string containing the output of a command, or log an
error.

=cut

sub capture_string {
  my ($cmd, @args) = @_;

  my $output;

  eval {
    $output = capturex($cmd, @args);
  };

  if ($@) {
    diag(1, 'error', $@);
  }

  return $output;
}

=item capture_list($cmd, @args)

Return a list containing the output of a command, or log an
error.

=cut

sub capture_list {
  my ($cmd, @args) = @_;

  my @output;

  eval {
    @output = capturex($cmd, @args);
  };

  if ($@) {
    diag(1, 'error', $@);
  }

  return @output;
}

=back

=head1 UTILITY SUBROUTINES

=over

=item parse_config($path_to_file)

Grab a hash of configuration options out of some text file,
formatted like so:

    key1=value
    key2=value2

=cut

sub parse_config {
  my %config;
  my ($config_path) = @_;
  my $config_str = get_file($config_path);

  # Crude dos2unix:
  $config_str =~ s/\r\n/\n/g;

  while ($config_str =~ m{^([a-z_]+) = (.*?)$}ixmg) {
    my $key = $1;
    my $value = $2;
    $config{$key} = $value;
  }

  return %config;
}

=item get_file($path_to_file)

Returns the contents of a given file as a string.

=cut

sub get_file {
  my ($path) = @_;

  if (! -e $path) {
    die "$path doesn't appear to exist."
  }

  local $/ = undef;
  open my $fh, '<', $path
    or die "Failed opening $path: $!"; 
  my $contents = <$fh>;
  close $fh;

  return $contents;
}

=item put_file($path, $content)

Put $content in the file at $path.

=cut

sub put_file {
  my ($path, $content, $backup_path) = @_;

  # Handle one-time backups - this could use some rethinking.
  if (defined $backup_path) {
    if (! -e $backup_path) {
      if (-e $path) {
        my $old_contents = get_file($path);
        put_file($backup_path, $old_contents);
      }
    }
  }

  open my $fh, '>', $path
    or die "Failed opening $path: $!"; 
  print $fh $content;
  close $fh;
}

=item get_blurb()

Return a useful blurb for inclusion in config file comments.

=cut

sub get_blurb {
  return "This file is managed by $OCCI_CONFIG";
}

=item diag(@columns)

Print columns of diagnostic output.

=cut

sub diag {
  my ($depth, @cols) = @_;
  print "\t" x $depth;
  print join " :: ", @cols;
  print "\n";
}

=item have_config_or_die($path)

Check that a given config file exists, and exit with some documentation if not.

=cut

sub have_config_or_die {
  my ($file) = @_;

  return 1 if -f $file;

  print <<"HELPTEXT";
It looks like you don't have a $OCCI_CONFIG yet.

In order to create one:

    sudo nano $OCCI_CONFIG

And then add configuration keys like:

    hostname=somepi

See /usr/share/doc/occi/occidentalis_example.txt for a full example.
HELPTEXT

  exit 1;
}


=back

=head1 AUTHOR

Brennen Bearnes
Todd Treece

=head1 COPYING

The MIT License (MIT)

Copyright (c) 2015 Adafruit Industries

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.

=cut
